/**
 * A test for the timeout functionality in Lingua Franca.
 *
 * @author Maiko Brants TU Dresden
 *
 * Modeled after the C version of this test.
 */
target Cpp {
  timeout: 11 msec
}

import Sender from "lib/LoopedActionSender.lf"

reactor Consumer {
  input in: int
  state success: bool = false

  logical action after_shutdown: void

  reaction(in) {=
    auto current{get_elapsed_logical_time()};
    if(current > 11ms ){
      reactor::log::Error() << "Received at: " << current.count() << ". Failed to enforce timeout.";
      exit(1);
    } else if(current == 11ms) {
      success=true;
    }
  =}

  reaction(shutdown) -> after_shutdown {=
    reactor::log::Info() << "Shutdown invoked at tag " << get_tag();
    if((get_elapsed_logical_time() == 11ms ) && get_microstep() == 0 && (success == true)){
      reactor::log::Info() << "SUCCESS: successfully enforced timeout.";
      after_shutdown.schedule();
    } else {
      reactor::log::Error() << "Failed to enforce timeout at the correct tag.";
      exit(1);
    }
  =}

  reaction(after_shutdown) {=
    reactor::log::Error() << "Executed a reaction after timeout.";
    exit(2);
  =}
}

main reactor Timeout_Test {
  consumer = new Consumer()
  producer = new Sender(take_a_break_after=10, break_interval = 1 msec)

  producer.out -> consumer.in
}
