/*
 * A test for the request_stop() functionality in Lingua Franca.
 * This version of the test is threaded.
 * 
 * @author Soroush Bateni
 */
 target C {
    timeout: 11 msec,
    build-type: RelWithDebInfo,
    // logging: DEBUG
};

import Sender from "../lib/LoopedActionSender.lf"

reactor Consumer {
    input in:int;
    state reaction_invoked_correctly:bool(false);
    reaction(in) {=
        tag_t current_tag = get_current_tag();
        if (compare_tags(current_tag,
                         (tag_t) { .time = MSEC(10) + get_start_time(), .microstep = 9}) > 0) {
            // The reaction should not have been called at tags larger than (10
            // msec, 9)
            char time[255];
            error_print_and_exit("Invoked reaction(in) at tag (%llu, %d) which is bigger than shutdown.", 
                current_tag.time - get_start_time(), current_tag.microstep);
        } else if (compare_tags(current_tag,
                         (tag_t) { .time = MSEC(10) + get_start_time(), .microstep = 8}) == 0) {
            // Call request_stop() at relative tag (10 msec, 8)
            info_print("Requesting stop.");
            request_stop();
        } else if (compare_tags(current_tag,
                         (tag_t) { .time = MSEC(10) + get_start_time(), .microstep = 9}) == 0) {
            // Check that this reaction is indeed also triggered at (10 msec, 9)
            // printf("Reaction invoked.\n");
            self->reaction_invoked_correctly = true;
        }
    =}
    
    reaction(shutdown) {=
        tag_t current_tag = get_current_tag();
        info_print("Shutdown invoked at tag (%lld, %u).", current_tag.time - get_start_time(), current_tag.microstep);
        // Check to see if shutdown is called at relative tag (10 msec, 9)
        if (compare_tags(current_tag,
            (tag_t) { .time = MSEC(10) + get_start_time(), .microstep = 9}) == 0 && 
            self->reaction_invoked_correctly == true) {
            info_print("SUCCESS: successfully enforced stop.");
        } else if(compare_tags(current_tag,
            (tag_t) { .time = MSEC(10) + get_start_time(), .microstep = 9}) > 0) {
                error_print_and_exit("Shutdown invoked at tag (%llu, %d). Failed to enforce timeout.",
                            current_tag.time - get_start_time(), current_tag.microstep);
        } else if (self->reaction_invoked_correctly == false) {
            // Check to see if reactions were called correctly
        	error_print_and_exit("Failed to invoke reaction(in) at tag (%llu, %d).",
                            current_tag.time - get_start_time(), current_tag.microstep);
        }
    =}
}

main reactor {
   consumer = new[4] Consumer();
   producer = new[4] Sender(break_interval = 1 msec); 
   
   producer.out -> consumer.in;
}
