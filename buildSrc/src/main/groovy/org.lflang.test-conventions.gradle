plugins {
    id 'java-test-fixtures'
    id 'jacoco'
}

jacoco {
    toolVersion = jacocoVersion
}



dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testImplementation "org.junit.platform:junit-platform-commons:$jUnitPlatformVersion"
    testImplementation "org.junit.platform:junit-platform-engine:$jUnitPlatformVersion"
    testImplementation "org.opentest4j:opentest4j:$openTest4jVersion"
    testImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:$xtextVersion"
    testImplementation "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:$xtextVersion"

    testFixturesImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testFixturesImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testFixturesImplementation "org.junit.platform:junit-platform-commons:$jUnitPlatformVersion"
    testFixturesImplementation "org.junit.platform:junit-platform-engine:$jUnitPlatformVersion"
    testFixturesImplementation "org.opentest4j:opentest4j:$openTest4jVersion"
    testFixturesImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:$xtextVersion"
    testFixturesImplementation "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:$xtextVersion"
}

test {
    useJUnitPlatform {
        excludeTags 'Integration'
    }
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    failFast = true

    workingDir = rootProject.projectDir
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = file("${buildDir}/reports/html/jacoco")
        xml.outputLocation = file("${buildDir}/reports/xml/jacoco")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/org/lflang/services/**',
                              '**/org/lflang/lf/**',
                              '**/org/lflang/serializer/**',
                              '**/org/lflang/parser/antlr/**'
                    ]
            )
        }))
    }
}



tasks.register('integration', Test) {
    if (System.getProperty('integrationInclude') != null) {
        filter {
            includeTestsMatching System.getProperty('integrationInclude')
        }
    }
    useJUnitPlatform {
        includeTags 'Integration'

    }
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    // Pass the scheduler and runtime property on to the Java VM
    systemProperty 'scheduler', System.getProperty('scheduler')
    systemProperty 'runtime', System.getProperty('runtime')

    failFast = true

    workingDir = rootProject.projectDir
}